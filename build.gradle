buildscript {
    ext {
        springBootVersion = '2.3.10.RELEASE'
        springDependencyManagementVersion = '1.0.11.RELEASE'
        spotlessVersion = '5.12.4'
        sonarqubeVersion = '3.1.1'
        openApiVersion = '1.5.8'
        springCloudVersion = 'Hoxton.SR11'
        groovyVersion = '2.5.14'
        spockVersion = '1.3-groovy-2.5'
        testcontainersVersion = '1.15.3'
    }
}

plugins {
    id 'idea'
    id 'org.springframework.boot' version "$springBootVersion"
    id 'io.spring.dependency-management' version "$springDependencyManagementVersion"
    id "com.diffplug.spotless" version "$spotlessVersion"
    id 'java'
    id 'groovy'
    id 'org.sonarqube' version "$sonarqubeVersion"
}

version = '1.0-SNAPSHOT'
group = 'com.sportphotos'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion"
        mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation "org.springdoc:springdoc-openapi-ui:$openApiVersion"
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation "org.spockframework:spock-core:$spockVersion"
    testImplementation "org.spockframework:spock-spring:$spockVersion"
    testImplementation group: 'org.codehaus.groovy', name: 'groovy-all', version: "$groovyVersion", ext: 'pom'
    testImplementation 'org.testcontainers:mongodb'
}

sonarqube {
    properties {
        property 'sonar.sourceEncoding', 'UTF-8'
        property 'sonar.projectKey', 'Sport-Photos_sport-photos-backend'
        property 'sonar.organization', 'sport-photos'
        property 'sonar.host.url', 'https://sonarcloud.io'
    }
}

spotless {
    java {
        googleJavaFormat()
    }
}

sourceSets {
    test.groovy.srcDirs(
            'src/test/common',
            'src/test/unit',
            'src/test/integration',
            'src/test/functional'
    )
}
